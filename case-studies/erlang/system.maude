mod SYSTEM is

  protecting META-LEVEL .
	protecting MAUDE-SYNTAX-UPDOWN .
	protecting SEM_ENTITY .
  protecting SEM_MESSAGE .
  protecting SEM_PID .
  protecting SEM_MODENV .
  protecting SEM_PROCESS .

  sort Sys .
  subsort Entity < Sys .

	op #empty-system : -> Sys [ctor] .
	op _||_ : Sys Sys -> Sys [ctor assoc comm id: #empty-system prec 90 format(d sm! o d) ] .

	*******************************
  *** METAREPRESENTATION PART ***
	*******************************

	op #downSystem : Term -> Sys [memo] .

  var E : Entity .
  var SYS : Sys .
  var EX : Exp .
  var ME : ModEnv .
	var SUB : Substitution .
	vars T T1 T2 : Term .
  var ENTRYPOINT : FunName .
  var MOD : FunDefSeq .
  var ARGS : Args .
  var EXSEQ : NeExpSeq .
  var ENV : EnvStack .

	eq #up(#empty-system) = '#empty-system.Sys .
	ceq #up(E || SYS) = '_||_[#up(E), #up(SYS)]
     if SYS =/= #empty-system .

	eq #downSystem('#empty-system.Sys) = #empty-system .
	ceq #downSystem(T) = #downEntity(T1) || #downSystem(T2)
  if SUB := metaMatch(GRAMMAR, '_||_['E:Entity, 'SYS:Sys], T, nil, 0)
		/\ 'E:Entity <- T1 ; 'SYS:Sys <- T2 := SUB .

  *******************************
  ******** INIT FUNCTION ********
	*******************************

  op #setup-first-proc : Sys FunName FunDefSeq GroundValues -> Sys .
  ceq #setup-first-proc(SYS, ENTRYPOINT, MOD, ARGS)
      = < pid(0) | exp: EXSEQ, mod-env: ME, env-stack: ENV > || pidGen(0)
      if ME := #extract-functions(MOD) /\
         EXSEQ := #get-function(ENTRYPOINT, ME) /\
         ENV := #init-env(#get-formal-args(ENTRYPOINT, ME), ARGS) .

endm
