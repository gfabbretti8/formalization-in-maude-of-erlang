fmod RICHTERMS is

  protecting META-LEVEL .

  sort TermWithKey .
  op _*_ : TermList Qid -> TermWithKey [ctor prec 81].

  sort TermIdPlKeys .
  op _x_x_x_ : GTermList Nat GTermList Qid -> TermIdPlKeys [ctor prec 81] .

endfm

view TermWithKey from TRIV to RICHTERMS is
  sort Elt to TermWithKey .
endv

view TermIdPlKeys from TRIV to RICHTERMS is
  sort Elt to TermIdPlKeys .
endv

fmod PRODUCE-FWD-SEM is

  protecting RICHTERMS .
  protecting META-LEVEL .
  protecting HELPER .
  protecting QID .
  protecting META-TERM .
  protecting NAT .
  inc FOLD{TermWithKey} .
  inc FOLD{TermIdPlKeys} .

  vars K T T' T'' T1 T2 T2' T3 : Term .
  vars TERMS PL PL1 TERMS' TERMS'' : TermList .
  vars M M' : Module .
  var SUB : Substitution .
  var H : Header .
  vars I I' : Import .
  var IL : ImportList .
  var SS : SortSet .
  var SSDS : SubsortDeclSet .
  var OPDS : OpDeclSet .
  var MAS : MembAxSet .
  var EQS : EquationSet .
  var ATT : AttrSet .
  vars OPNAME FKEY KEY KEY'   : Qid .
  var RL : Rule .
  var RLL : RuleList .
  vars RLS RLS' : RuleSet .
  vars RLCOND : Condition .
  vars ID ID' ID'' : Nat .

  op #produce-fwd-sem : Module -> Module .
  eq #produce-fwd-sem(mod H is IL sorts SS . SSDS OPDS MAS EQS RLS endm) =
    mod H is
     #transform-import-list(IL)
     sorts SS .
     SSDS
     OPDS
     MAS
     EQS
     #transform-rls(#set-to-list(RLS))
    endm .

  *** On the import list we update the module 'SEM_ENTITY with
  *** 'SEM_ENTITY_WITH_KEY

  op #transform-import-list : ImportList -> ImportList .
  eq #transform-import-list(IL)
     = (protecting 'SEM_ENTITY_WITH_KEY .) IL .

  *** We iterate on the rules available and we transform each one of them.

  op #transform-rls : RuleList -> RuleSet .
  ceq #transform-rls(RL, RLL)
     = #transform-rl-crl(RL) #transform-rls(RLL)
     if RLL =/= #empty-list .
  eq #transform-rls(RL) = #transform-rl-crl(RL) [owise] .

  *** The following function analyses the kind of rule (if conditional or not)
  *** and then proceeds to the appropriate transformation. The only difference
  *** is that in a conditional rule we must also consider the if clause.

  op #transform-rl-crl : Rule -> Rule .
  eq #transform-rl-crl(rl T1 => T2 [ATT] .)
     = #transform-rl(rl T1 => T2 [ATT] .) .
  eq #transform-rl-crl(crl T1 => T2 if RLCOND [ATT] .)
     = #transform-crl(crl T1 => T2 if RLCOND [ATT] .) .

  op #transform-rl : Rule -> Rule .
  eq #transform-rl(rl T1 => T2 [ATT] .)
     = #call-right-transform( #transform-rl-left-term(T1, 'nil), T2, ATT) .

  op #transform-crl : Rule -> Rule .
  eq #transform-crl(crl T1 => T2 if RLCOND [ATT] .)
     = #call-right-transform(#transform-rl-left-term(T1, 'nil), T2, RLCOND, ATT) .

  ****  The following equations perform a depth first visit on the subtree of
  ****  the left term of the rule. During such visit each entity is tagged with
  ****  a key (actually a variable that represents it) and each key used
  ****  is collected and then returned. Keys must be returned so that we can put
  ****  in relations the elements of left side with the ones in the right side.

  op #call-right-transform : TermWithKey Term AttrSet -> Rule .
  ceq #call-right-transform(T1 * KEY, T2, ATT)
     = rl T1 => '_||_[TERMS, '`[_;_`][T1,TERMS']] [ATT] .
     if TERMS x ID x TERMS' x KEY := #transform-rl-right-term(T2, 0, KEY) .

  op #call-right-transform : TermWithKey Term Condition AttrSet  -> Rule .
  ceq #call-right-transform(T1 * KEY, T2, RLCOND, ATT)
     = crl T1 => '_||_[TERMS, '`[_;_`][T1,TERMS']] if RLCOND [ATT] .
     if TERMS x ID x TERMS' x KEY := #transform-rl-right-term(T2, 0, KEY) .

	********************************
  *** LEFT TERM TRANSFORMATION ***
	********************************

  *** #transform-rl-left-term checks the form of the term that appears in
  *** the tree of left terms of the rule, two possible scenarios exist.
  *** The first one, when declared-in is true, is when the term under
  *** analysis is a top-level operator, declared inside the module SYSTEM.
  *** In that case we must proceed recursively on its arguments, in fact the goal
  *** here is to tag each single entity.
  *** In the other scenario, when declared-in is false, we are dealing with an
  *** Entity of the lower level of the syntax, in that case we must tag it with
  ***  a key.

  op f : -> Func{Term, TermWithKey} .
  ceq f[T, (TERMS * KEY)] = (TERMS, TERMS') * KEY'
     if TERMS' * KEY' := #transform-rl-left-term(T, KEY) .

  op #transform-rl-left-term : Term QidList -> TermWithKey .
  ceq #transform-rl-left-term(OPNAME[TERMS], KEY) = (OPNAME[TERMS']) * KEY'
    if #declared-in(upModule('SYSTEM, false), OPNAME) /\
       TERMS' * KEY' := foldl-term(TERMS, (empty * KEY), f) .
  ceq #transform-rl-left-term(T, KEY)
     =  ('_*_[T,'key_[FKEY]]) * (FKEY)
    if FKEY := #freshKey(KEY) .

  op #freshKey : Qid -> Qid .
  eq #freshKey('nil) = 'L:NatList .
  eq #freshKey(KEY) = qid( substr(string(KEY), 0, find(string(KEY), ":", 0)) + "'" + ":NatList") .

	*********************************
  *** RIGHT TERM TRANSFORMATION ***
	*********************************

  *** The following set of equations transform the right side of the rule.
  *** The only difference w.r.t. the left side is that we need to freshly
  *** generate a new id

  op h : -> Func{Term, TermIdPlKeys} .
  ceq h[T, TERMS x ID x PL x KEY] = (TERMS,TERMS') x ID' x (PL, PL1) x KEY
      if TERMS' x ID' x PL1 x KEY := #transform-rl-right-term(T, ID + 1, KEY) .

  op #transform-rl-right-term : Term Nat Qid -> TermIdPlKeys .
  ceq #transform-rl-right-term(OPNAME[TERMS], ID, KEY)
    = OPNAME[TERMS'] x ID x OPNAME[PL] x KEY
    if #declared-in(upModule('SYSTEM, false), OPNAME) /\
       TERMS' x ID' x PL x KEY := foldl-term(TERMS, (empty x ID x empty x KEY), h) .

  eq #transform-rl-right-term(T, ID, KEY)
      = '_*_[T, 'key_['__[upTerm(ID), KEY]]]
        x ID
        x '@:_['key_['__[upTerm(ID), KEY]]]
        x KEY .

endfm
