mod SEM_COREBIB is
	protecting NAT .
	protecting STRING .
	protecting SEM_ENTITIES .
  protecting SEM_STORE .
  protecting SEM_MODENV .
  protecting SEM_LABEL .
  protecting SEM_NORMALISATION .


  var ARGS : GroundValues .
  var STORESTACK STORESTACK' : StoreStack .
  var ME : ModEnv .
  var FUNNAME : FunName .
  var INT : Int .
  var REQLABEL : RequestLabel .
  var LABEL : Label .
  vars EX1 EX2 EX1' EX EX' : Exp .
  var PID : ErlInt .
  var PAT : Pattern .
  var STORE STORE' : Store .
  var PAYLOAD : GroundValues .
  var CLSEQ : ClauseSeq .
  var EXSEQ : NeExpSeq .

  eq [spawn] :
    < req-spawn(pid(INT)), STORESTACK, atom("erlang") : atom("spawn")(FUNNAME, ARGS)> =
    < spawn(FUNNAME,ARGS), STORESTACK, int(INT) > .

  eq [send] :
    < REQLABEL, STORE : STORESTACK, int(INT) ! PAT > =
    < pid(INT) ! #map-args(PAT, STORE), STORE : STORESTACK, #map-args(PAT, STORE) > .

  eq [self] :
    < self(pid(INT)), STORESTACK, atom("self")() > =
    < tau, STORESTACK, int(INT) > .

  ceq [send] :
    < REQLABEL, STORESTACK, PID ! EX > =
    < LABEL, STORESTACK', PID ! EX' >
    if < LABEL, STORESTACK', EX' > :=
       < REQLABEL, STORESTACK, EX > .

  ceq [send] :
    < REQLABEL, STORESTACK, EX1 ! EX2 > =
    < LABEL, STORESTACK, EX1' ! EX2 >
    if < LABEL, STORESTACK, EX1' > :=
       < REQLABEL, STORESTACK, EX1 > .

  ceq [receive] :
    < req-receive(PAYLOAD), STORE : STORESTACK, receive CLSEQ end > =
    < received, STORE' : (STORE : STORESTACK), begin EXSEQ end >
    if #entityMatchSuccess(EXSEQ | STORE') := #entityMatch(CLSEQ ; #empty-clauselist | PAYLOAD | STORE ) .

endm
