fmod CINNI is
  protecting QID .

  *** Channel definition
  sort Chan .
  op _{_} : Qid Nat -> Chan [ctor prec 1] .

  *** Calculus independent equations of CINNI
  sort Subst .
  op [_:=_] : Qid Chan -> Subst .
  op [shift_] : Qid -> Subst .
  op [lift__] : Qid Subst -> Subst .
  op __ : Subst Chan -> Chan .

  vars a b : Qid .
  var n : Nat .
  var x : Chan .
  var S : Subst .

  eq [a := x] a{0} = x .
  eq [a := x] a{s(n)} = a{n} .
  ceq [a := x] b{n} = b{n} if a =/= b .

  eq [shift a] a{n} = a{s(n)} .
  ceq [shift a] b{n} = b{n} if a =/= b .

  eq [lift a S] a{0} = a{0} .
  eq [lift a S] a{s(n)} = [shift a] S a{n} .
  ceq [lift a S] b{n} = [shift a] S b{n} if a =/= b .
endfm
