***(

	SYSTEM.MAUDE

	This module defines the system level transitions. This is the rewriting theory
	that operates upon the equivalence classes that are defined by the underlying
	equational theory (given in the functional modules SEM_EQUATIONS and its submodules).

	In contrast to the oriented equations and equational attributes that constitute
	the MEL theory, the rewriting rules do not have to terminate or to be Church Rosser.

	They represent the observable actions that the system can do. In our approach, we try
	to move as many local evaluation steps as possible into the equational theory. This
	reduces the search space significantly (for example, interleaving is not necessary
	if completely autonomous steps are taken within different processes).
	The rewriting rules that are specified here represent evaluation steps that may not
	be included in the equational theory as they imply side effects onto other processes.

	The transition rules of the "system"-level are collected in the SEM_TRANSITION
	module.

	The functional module that constitutes the equational theory includes the different
	sets of oriented equations and is called SEM_EQUATIONS.

	The whole theory, consisting of the rewriting rules and the equational theory
	is then given by the module THEORY.

***)

mod SEM_TRANSITIONS is
	protecting SYNTAX .
	protecting SEM_LABEL .
	protecting SEM_ENTITIES .
	protecting SEM_SYSLABEL .
  protecting SEM_MATCH_ENTITY .

	vars EX EX' EX1 EX2 : Exp .
	vars P P' : Pid .
	vars LINKS LINKS1 : PidSequence .
  var L : Literal .
	vars TRAP TRAP1 : Bool .
	vars INT INT1 : Int .
	vars ME ME1 ME' : ModEnv .
	vars A : Atom .
	var SL : SysLabel .
	var EL EL1 EL1' : Label .
	var PIDS : PidSequence .
	var RES : SysResult .
  var CLSEQ : ClauseSeq .
  var MSG : Message .
  var ASET : AttributeSet .
  var ENTS : Entities .


  var ENTRYPOINT : FunName .
  var MOD : FunDefSeq .


	*******************************
  ******** INIT FUNCTION ********
	*******************************
  ceq #setup-first-proc(ENTS, ENTRYPOINT, MOD) =
      < pid(0) | label: tau, sys-res: #no-res, exp: EX, mod-env: ME > || pid(0)
    if ME := #extract-functions(MOD) /\
       EX := #get-function(ENTRYPOINT, ME) .

	*******************************
  ******** SYSTEM RULES  ********
	*******************************

	crl [sys-spawn] :
        < P | label: spawn(A), sys-res: #no-res, mod-env: ME, ASET > || PIDS  =>
		 	  < P | label: tau, sys-res: #res-spawn(INT), mod-env: ME, ASET > ||
			  < pid(INT) | label: tau, sys-res: #no-res, exp: EX1, mod-env: ME > || pid(INT), PIDS
		if INT := #getNewPid(PIDS)
		/\ EX1 := #get-function(A, ME) [print P " sys-spawn " ASET ] .

	rl [sys-send] :
      < P | label: P' ! A, sys-res: #no-res, ASET >  =>
		  < P | label: tau, sys-res: #res-send(true), ASET > || < sender: P, receiver: P', payload: A >  [print P " sys-send" ].

  crl [sys-receive] :
      < P | label: receive CLSEQ, sys-res: #no-res, exp: EX, ASET > || MSG =>
      < P | sys-res: #no-res, exp: EX', ASET >
    if #entityMatchSuccess(EX' | MSG ) := #entityMatch(CLSEQ | MSG ) [print P " sys-receive" ] .
endm

mod SEM_EQUATIONS is
	protecting SYNTAX .
	protecting SEM_NORMALISATION .
  protecting SEM_COREBIB .
endm

mod THEORY is
	protecting SYNTAX .
	protecting SEM_EQUATIONS .
  protecting SEM_TRANSITIONS .
endm
