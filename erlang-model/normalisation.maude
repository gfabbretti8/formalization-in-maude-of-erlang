mod SEM_NORMALISATION is
	protecting SYNTAX .
  protecting HELP-SYNTAX .
  protecting SEM_MATCH_ENTITY .
  protecting SEM_MODENV .
  protecting SEM_LABEL .

  var FUNNAME : Atom .
  var REQLABEL : RequestLabel .
  vars LABEL LABEL' : StopLabel .
  vars STORE STORE' : Store .
  var EXPSEQ : NeExpSeq .
  var GVALUE GVALUE' : GroundValues .
  vars T1 T2 T3 T4 T5 T6 : Term .
  var VAR : Var .
  vars EXSEQ EXSEQ' : NeExpSeq .
  var EX EX' EX'' EX1 EX2 EX1' : Exp .
  var CLSEQ : ClauseSeq .
  var ME : ModEnv .
  var PAT : Pattern .
  var INT INT' RES : Int .
  var ARGS : Args .
  var STORESTACK STORESTACK' : StoreStack .


  sort ExpConf .
  op <_,_,_,_> : Label Store ExpSeq ModEnv -> ExpConf [ctor frozen] .

   ceq [var] :
    < REQLABEL, STORE : STORESTACK, VAR, ME > =
    < crashed, STORE : STORESTACK, VAR, ME >
    if STORE[VAR] == undefined .

   ceq [var] :
    < REQLABEL, STORE : STORESTACK, VAR, ME > =
    < tau, STORE : STORESTACK, STORE[VAR], ME >
    if STORE[VAR] =/= undefined .

    eq [match] :
     < REQLABEL, STORE : STORESTACK, GVALUE = GVALUE, ME > =
     < tau, STORE : STORESTACK, GVALUE, ME > .

   ceq [match] :
     < REQLABEL, STORE : STORESTACK, GVALUE = GVALUE', ME > =
     < crashed, STORE : STORESTACK, #crashed, ME >
     if GVALUE =/= GVALUE' .

   ceq [match] :
    < REQLABEL, STORE : STORESTACK, VAR = GVALUE, ME > =
    < tau, STORE' : STORESTACK, GVALUE, ME >
    if STORE[VAR] == undefined /\
       STORE' := insert(VAR, GVALUE, STORE) .

   ceq [match] :
    < REQLABEL, STORE : STORESTACK, VAR = GVALUE, ME > =
    < crashed, STORE : STORESTACK, #crashed, ME >
    if STORE[VAR] =/= undefined .

   ceq [match] :
    < REQLABEL, STORESTACK, VAR = EX, ME > =
    < LABEL, STORESTACK', VAR = EX', ME >
    if < LABEL, STORESTACK', EX', ME > :=
       < REQLABEL, STORESTACK, EX, ME > .

   ceq [minus] :
    < REQLABEL, STORESTACK, int(INT) - int(INT'), ME > =
    < tau, STORESTACK, int(RES), ME >
    if RES := downTerm(getTerm(metaReduce(upModule('INT, false), '_-_[upTerm(INT), upTerm(INT')])), INT) .

   ceq [minus] :
    < REQLABEL, STORESTACK, int(INT) - EX , ME > =
    < LABEL, STORESTACK', int(INT) - EX', ME >
    if < LABEL, STORESTACK', EX' , ME > :=
       < REQLABEL, STORESTACK, EX , ME > .

   ceq [minus] :
    < REQLABEL, STORESTACK, EX1 - EX2 , ME > =
    < LABEL, STORESTACK', EX1' - EX2 , ME >
    if < LABEL, STORESTACK', EX1' , ME > :=
       < REQLABEL, STORESTACK, EX1 , ME > .

   ceq [mult] :
    < REQLABEL, STORESTACK, int(INT) * int(INT'), ME > =
    < tau, STORESTACK, int(RES), ME >
    if RES := downTerm(getTerm(metaReduce(upModule('INT, false), '_*_[upTerm(INT), upTerm(INT')])), INT) .

   ceq [mult] :
    < REQLABEL, STORESTACK, int(INT) * EX , ME > =
    < LABEL, STORESTACK', int(INT) * EX', ME >
    if < LABEL, STORESTACK', EX' , ME > :=
       < REQLABEL, STORESTACK, EX , ME > .

   ceq [mult] :
    < REQLABEL, STORESTACK, EX1 * EX2 , ME > =
    < LABEL, STORESTACK', EX1' * EX2 , ME >
    if < LABEL, STORESTACK', EX1' , ME > :=
       < REQLABEL, STORESTACK, EX1 , ME > .

   ceq [case-succ] :
    < REQLABEL, STORE : STORESTACK, case GVALUE of CLSEQ end, ME > =
    < tau, STORE' : (STORE : STORESTACK), begin EXSEQ end, ME >
     if #entityMatchSuccess(EXSEQ | STORE') := #entityMatch(CLSEQ ; #empty-clauselist | GVALUE | STORE ) .

   eq [case-fail] :
    < REQLABEL, STORESTACK, case GVALUE of CLSEQ end, ME > =
    < crashed, STORESTACK, #crashed, ME > [owise] .

   ceq [case] :
    < REQLABEL, STORESTACK, case EX of CLSEQ end, ME > =
    < LABEL, STORESTACK', case EX' of CLSEQ end, ME >
    if < LABEL, STORESTACK', EX', ME > :=
       < REQLABEL, STORESTACK, EX, ME > .

   ceq [funcall] :
    < REQLABEL, STORE : STORESTACK, FUNNAME(ARGS), ME > =
    < tau, STORE' : (STORE : STORESTACK), begin EXSEQ end, ME >
    if STORE' := #init-store(#get-formal-args(FUNNAME, ME),#map-args(ARGS,STORE)) /\
        EXSEQ := #get-function(FUNNAME, ME) .

   ceq [funcall] :
    < REQLABEL, STORESTACK, FUNNAME(EXSEQ), ME > =
    < LABEL, STORESTACK', FUNNAME(EXSEQ'), ME >
    if < LABEL, STORESTACK', EXSEQ', ME > :=
       < REQLABEL, STORESTACK, EXSEQ, ME > .

   eq [begin] :
    < REQLABEL, STORE : STORESTACK, begin GVALUE end, ME > =
    < tau, STORESTACK, GVALUE, ME > .

   ceq [begin] :
    < REQLABEL, STORESTACK, begin EXSEQ end, ME > =
    < LABEL, STORESTACK', begin EXSEQ' end, ME >
    if < LABEL, STORESTACK', EXSEQ', ME > :=
       < REQLABEL, STORESTACK, EXSEQ, ME > .

   ceq [seq] :
    < REQLABEL, STORESTACK, EX, EXSEQ, ME > =
    < LABEL, STORESTACK', EX', EXSEQ, ME >
    if < LABEL, STORESTACK', EX', ME > :=
       < REQLABEL, STORESTACK, EX, ME > /\
       not(EX :: GroundValues ).

   eq [seq] :
    < REQLABEL, STORESTACK, GVALUE, EXPSEQ, ME > =
    < tau, STORESTACK, EXPSEQ, ME > .

endm

