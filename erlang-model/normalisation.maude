mod SEM_NORMALISATION is
	protecting SYNTAX .
  protecting HELP-SYNTAX .
  protecting SEM_MATCH_ENTITY .
  protecting SEM_MODENV .
  protecting SEM_LABEL .

  var FUNNAME : Atom .
  var REQLABEL : RequestLabel .
  vars LABEL LABEL' : StopLabel .
  vars STORE STORE' : Store .
  var EXPSEQ : NeExpSeq .
  var GVALUE GVALUE' : GroundValues .
  vars T1 T2 T3 T4 T5 T6 : Term .
  var VAR : Var .
  vars EXSEQ EXSEQ' : NeExpSeq .
  var EX EX' EX'' EX1 EX2 EX1' : Exp .
  var CLSEQ : ClauseSeq .
  var ME : ModEnv .
  var PAT : Pattern .
  var INT INT' RES : Int .
  var ARGS : Args .
  var STORESTACK STORESTACK' : StoreStack .


  sort ExpConf .
  op <_,_,_> : Label Store ExpSeq -> ExpConf [ctor frozen] .

   ceq [var] :
    < REQLABEL, STORE : STORESTACK, VAR > =
    < crashed, STORE : STORESTACK, VAR >
    if STORE[VAR] == undefined .

   ceq [var] :
    < REQLABEL, STORE : STORESTACK, VAR > =
    < tau, STORE : STORESTACK, STORE[VAR] >
    if STORE[VAR] =/= undefined .

    eq [match] :
     < REQLABEL, STORE : STORESTACK, GVALUE = GVALUE > =
     < tau, STORE : STORESTACK, GVALUE > .

   ceq [match] :
     < REQLABEL, STORE : STORESTACK, GVALUE = GVALUE' > =
     < crashed, STORE : STORESTACK, #crashed >
     if GVALUE =/= GVALUE' .

   ceq [match] :
    < REQLABEL, STORE : STORESTACK, VAR = GVALUE > =
    < tau, STORE' : STORESTACK, GVALUE >
    if STORE[VAR] == undefined /\
       STORE' := insert(VAR, GVALUE, STORE) .

   ceq [match] :
    < REQLABEL, STORE : STORESTACK, VAR = GVALUE > =
    < crashed, STORE : STORESTACK, #crashed >
    if STORE[VAR] =/= undefined .

   ceq [match] :
    < REQLABEL, STORESTACK, VAR = EX > =
    < LABEL, STORESTACK', VAR = EX' >
    if < LABEL, STORESTACK', EX' > :=
       < REQLABEL, STORESTACK, EX > .

   ceq [minus] :
    < REQLABEL, STORESTACK, int(INT) - int(INT') > =
    < tau, STORESTACK, int(RES) >
    if RES := downTerm(getTerm(metaReduce(upModule('INT, false), '_-_[upTerm(INT), upTerm(INT')])), INT) .

   ceq [minus] :
    < REQLABEL, STORESTACK, int(INT) - EX > =
    < LABEL, STORESTACK', int(INT) - EX' >
    if < LABEL, STORESTACK', EX' > :=
       < REQLABEL, STORESTACK, EX > .

   ceq [minus] :
    < REQLABEL, STORESTACK, EX1 - EX2 > =
    < LABEL, STORESTACK', EX1' - EX2 >
    if < LABEL, STORESTACK', EX1' > :=
       < REQLABEL, STORESTACK, EX1 > .

   ceq [mult] :
    < REQLABEL, STORESTACK, int(INT) * int(INT') > =
    < tau, STORESTACK, int(RES) >
    if RES := downTerm(getTerm(metaReduce(upModule('INT, false), '_*_[upTerm(INT), upTerm(INT')])), INT) .

   ceq [mult] :
    < REQLABEL, STORESTACK, int(INT) * EX > =
    < LABEL, STORESTACK', int(INT) * EX' >
    if < LABEL, STORESTACK', EX' > :=
       < REQLABEL, STORESTACK, EX > .

   ceq [mult] :
    < REQLABEL, STORESTACK, EX1 * EX2 > =
    < LABEL, STORESTACK', EX1' * EX2 >
    if < LABEL, STORESTACK', EX1' > :=
       < REQLABEL, STORESTACK, EX1 > .

   ceq [case-succ] :
    < REQLABEL, STORE : STORESTACK, case GVALUE of CLSEQ end > =
    < tau, STORE' : (STORE : STORESTACK), begin EXSEQ end >
     if #entityMatchSuccess(EXSEQ | STORE') := #entityMatch(CLSEQ ; #empty-clauselist | GVALUE | STORE ) .

   eq [case-fail] :
    < REQLABEL, STORESTACK, case GVALUE of CLSEQ end > =
    < crashed, STORESTACK, #crashed > [owise] .

   ceq [case] :
    < REQLABEL, STORESTACK, case EX of CLSEQ end > =
    < LABEL, STORESTACK', case EX' of CLSEQ end >
    if < LABEL, STORESTACK', EX' > :=
       < REQLABEL, STORESTACK, EX > .

   ceq [funcall] :
    < funcall(ME), STORE : STORESTACK, FUNNAME(ARGS) > =
    < tau, STORE' : (STORE : STORESTACK), begin EXSEQ end >
    if STORE' := #init-store(#get-formal-args(FUNNAME, ME),#map-args(ARGS,STORE)) /\
        EXSEQ := #get-function(FUNNAME, ME) .

   ceq [funcall] :
    < REQLABEL, STORESTACK, FUNNAME(EXSEQ) > =
    < LABEL, STORESTACK', FUNNAME(EXSEQ') >
    if < LABEL, STORESTACK', EXSEQ' > :=
       < REQLABEL, STORESTACK, EXSEQ > .

   eq [begin] :
    < REQLABEL, STORE : STORESTACK, begin GVALUE end > =
    < tau, STORESTACK, GVALUE > .

   ceq [begin] :
    < REQLABEL, STORESTACK, begin EXSEQ end > =
    < LABEL, STORESTACK', begin EXSEQ' end >
    if < LABEL, STORESTACK', EXSEQ' > :=
       < REQLABEL, STORESTACK, EXSEQ > .

   ceq [seq] :
    < REQLABEL, STORESTACK, EX, EXSEQ > =
    < LABEL, STORESTACK', EX', EXSEQ >
    if < LABEL, STORESTACK', EX' > :=
       < REQLABEL, STORESTACK, EX > /\
       not(EX :: GroundValues ).

   eq [seq] :
    < REQLABEL, STORESTACK, GVALUE, EXPSEQ > =
    < tau, STORESTACK, EXPSEQ > .

endm

