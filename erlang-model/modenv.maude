***(

	MODENV.MAUDE

	In Core-Erlang, functions are normally defined within the context of a module.
	The module consists mainly of a function definition list (i.e. a whitespace
	separated list of function definitions) and some attributes that specify, for
	example, which functions may be called from outside the module.
	Note: We do not enforce this when evaluating an inter-module call as such a
	      call would lead to a compile time error.
	The functional (Maude) module SEM_MODENV defines the operations needed to
	deal with the set of functions that are known to the system when evaluating
	processes.
	Functions can be included in this set by loading Core-Erlang modules into
	the Maude system using the add-module command.
	Additionally, we define operations to extract functions from the set and
	to convert the set representation back to a list of function definitions
	(corresponding to the Core-Erlang syntax).

***)

fmod SEM_MODENV is
	protecting SYNTAX .
	protecting SYNTAX-COMMON .
	protecting MAUDE-SYNTAX-UPDOWN .

	sort ModEnvMap .
	op _==>_ : FunName ExpSeq -> ModEnvMap [ctor prec 70] .
	op _[_]==>_ : FunName Args ExpSeq -> ModEnvMap [ctor prec 70] .

  sort ModEnv .
	subsort ModEnvMap < ModEnv .

	op #empty-modenv : -> ModEnv [ctor] .
	op _,_ : ModEnv ModEnv -> ModEnv [ctor assoc comm prec 71 id: #empty-modenv] .

	var FUNBODY : ExpSeq .
	var MN : Atom .
  var FDEF : FunDef .
	var FDLIST : FunDefSeq .
	var FD : FunDef .
	var FNAME FNAME1 FNAME2 : Atom .
	var FARITY : ErlInt .
	var FN : FunName .
	var MAP : ModEnv .
  var EX : Exp .
  var ARGS : Args .
  var PAT : Pattern .
  var TAIL : Pattern .

	op #extract-functions : FunDefSeq -> ModEnv .

	eq #extract-functions(FDEF FDLIST)
     = #extract-functions(FDEF), #extract-functions(FDLIST) .
	eq #extract-functions(fun FNAME ()-> FUNBODY .)
		= FNAME ==> FUNBODY .
	eq #extract-functions(fun FNAME (ARGS) -> FUNBODY .)
		= FNAME[ARGS] ==> FUNBODY .

	op #get-function : Atom ModEnv -> ExpSeq .

  eq #get-function(FNAME, FNAME ==> FUNBODY, MAP) = FUNBODY, #bottom .
  eq #get-function(FNAME, FNAME[ARGS] ==> FUNBODY, MAP) = FUNBODY, #bottom .
  eq #get-function(FNAME1, FNAME2 ==> FUNBODY, MAP)
     = #get-function(FNAME1, MAP) [owise] .

  op #empty : -> Pattern .

  op #get-formal-args : Atom ModEnv -> ExpSeq .
  eq #get-formal-args(FNAME, FNAME ==> FUNBODY, MAP) = #empty .
  eq #get-formal-args(FNAME, FNAME[ARGS]==> FUNBODY, MAP) = ARGS .
  eq #get-formal-args(FNAME1, FNAME2 ==> FUNBODY, MAP)
     = #get-function(FNAME1, MAP) .


	*******************************
  *** METAREPRESENTATION PART ***
	*******************************

	op #up : ModEnv -> Term [memo] .
	op #up : ModEnvMap -> Term [memo] .

	op #downModEnv : Term -> ModEnv [memo] .
 	op #downModEnvMap : Term -> ModEnv [memo] .

	var EXSEQ : ExpSeq .
	vars T T1 T2 T3 : Term .
	var SUB : Substitution .
  var ME : ModEnv .

	*** Meta-representing the module environment:
	eq #up(#empty-modenv) = '#empty-modenv.ModEnv .
	eq #up(FNAME ==> EXSEQ) = '_==>_[#up(FNAME), #up(EXSEQ)] .
	eq #up(FNAME[ARGS] ==> EXSEQ) = '_`[_`]==>_[#up(FNAME), #up(ARGS), #up(EXSEQ)] .
	eq #up(FNAME ==> EXSEQ, ME) = '_`,_[#up(FNAME ==> EXSEQ), #up(ME)] .
	eq #up(FNAME[ARGS] ==> EXSEQ, ME) = '_`,_[#up(FNAME[ARGS] ==> EXSEQ), #up(ME)] .

	*** Lowering the meta-representation level of module environments:
	eq #downModEnvMap('_==>_[T1,T2]) = #downExp(T1) ==> #downExpSeq(T2) .
	eq #downModEnvMap('_`[_`]==>_[T1,T2,T3]) = #downExp(T1)[#downExpSeq(T2)] ==> #downExpSeq(T3) .
	eq #downModEnv('#empty-modenv.ModEnv) = #empty-modenv .
	ceq #downModEnv(T) = #downModEnvMap(T1),  #downModEnv(T2)
		if SUB := metaMatch(GRAMMAR, '_`,_['MAP:ModEnvMap, 'ME:ModEnv], T, nil, 0)
		/\ 'MAP:ModEnvMap <- T1 ; 'ME:ModEnv <- T2 := SUB .
endfm
